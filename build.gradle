buildscript {
    apply plugin: 'java'

    ext {
        appGroup = "org.syaku.toy"
        appVersion = "1.0.0.BUILD-SNAPSHOT"
        springVersion = "5.1.5.RELEASE"
        springBootVersion = "2.1.2.RELEASE"
        lombokVersion = "1.18.2"
        restDocsVersion = "2.0.3.RELEASE"
    }

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.18"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "org.asciidoctor.convert"

idea {
    module {
        inheritOutputDirs = true
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

repositories {
    jcenter()
    mavenCentral()
}

group appGroup
version appVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.dependsOn(processResources)

configurations {
    providedRuntime
    compile.exclude module: "spring-boot-starter-tomcat"
}

wrapper {
    gradleVersion = '5.4'
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"

    compile "org.springframework.boot:spring-boot-starter-jetty"
    compile "org.springframework.boot:spring-boot-starter-validation"

    compile "org.springframework.boot:spring-boot-starter-security"
    testCompile "org.springframework.security:spring-security-test"

    compile "org.springframework.boot:spring-boot-starter-data-jpa"

    compileOnly "com.h2database:h2:1.4.191"
    // https://mariadb.com/kb/en/library/about-mariadb-connector-j/
    compile "org.mariadb.jdbc:mariadb-java-client:2.3.0"

    asciidoctor "org.springframework.restdocs:spring-restdocs-asciidoctor:${restDocsVersion}"
    testCompile "org.springframework.restdocs:spring-restdocs-mockmvc:${restDocsVersion}"

    compile "org.springframework.security.oauth:spring-security-oauth2:2.3.5.RELEASE"
    compile "org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.1.4.RELEASE"
    compile "org.springframework.boot:spring-boot-starter-oauth2-resource-server"

    // commons library
    testCompile "junit:junit:4.12"
    testCompile "org.jmock:jmock-junit4:2.8.2"
    testCompile "org.mockito:mockito-core:2.+"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    compile "org.apache.commons:commons-lang3:3.7"
    compile "org.modelmapper:modelmapper:2.3.0"
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

bootJar {
    enabled = true
    mainClassName = "org.syaku.toy.security.Application"
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    launchScript()
}
